[OpKindList 4.1d] // Экспорт данных произвел 0000BAV 12/04/2016 09:25:03 БД "-db /home2/bis/quit41d/db/bisquit,-ld bisquit,-L 200000,-n 500,-B 8000,-omsize 1400,-spin 10000,-U qbis,-P,-n 500"
// ***************************************************************************
sbk_acct:
DO TRANSACTION:
   /// <tr_package>
   /// 07.07PB.u07PB.sbk_acct
   /// </tr_package>

   /// <tr_name-opkind>
   /// PB: Счет картотеки (поиск/создание)
   /// </tr_name-opkind>

   /// <tr_details>
   /// Взвращает счет Картотеки К1, К2 или КБС. Если к р/с такая картотека не привязана, то создается новый счет.
   /// </tr_details>

   /// <tr_parent>
   /// u07PB
   /// </tr_parent>

   /// <tr_module>
   /// base
   /// </tr_module>

   /// <tr_proc>
   /// g-trans
   /// </tr_proc>

   /// <tr_DisplayForms>
   /// Нет
   /// </tr_DisplayForms>

   /// <tr_ProgressBar>
   /// Да
   /// </tr_ProgressBar>

   /// <tr_СС_ВыводНаЭкран>
   /// Да
   /// </tr_СС_ВыводНаЭкран>

   /// <tr_СС_ВыводПрткл>
   /// Нет
   /// </tr_СС_ВыводПрткл>

   // Библиотека функций для работы с картотеками.
   LIBRARY pcrd.

   DO BEFORE:
      // При вызове транзакции д.б. определены переменные:
      // @iAcct     - клиентский счет
      // @iCurrency - валюта кл.счета
      // @iCrtType  - тип картотеки: К1, К2, КБС или <пусто>
      // @iLog      - лог-файл
      
      // Возвращает значения:
      // @oAcctK    - счет нужной Картотеки
      // --@__currK   - валюта картотеки - всегда рубли
      
      @oAcctK = "";
      _if(CAN-DO("К1,К2,КБС,", @iCrtType), 1, 0);
   END BEFORE.

   10:
   DO TEMPLATE CLASS acctb ACTION Поиск ROLE Ввод данных:
      /// <tm_details>
      /// Р/с
      /// </tm_details>

      /// <tm_AttrOptimized>
      /// Нет
      /// </tm_AttrOptimized>

      // РЕКВИЗИТ: acct (формула) #1
      @acct = SEARCH(@iAcct)

      // РЕКВИЗИТ: currency (формула) #2
      @currency = SEARCH(@iCurrency)


      DO AFTER:
         @__branch  = @branch-id;
         @__custcat = @cust-cat;
         @__custid  = @cust-id;
         
         @oAcctK    = "";
         @__acctK1  = _if(@Карт1ВнСчет == ?, "", @Карт1ВнСчет);
         @__acctK2  = _if(@Карт2ВнСчет == ?, "", @Карт2ВнСчет);
         @__acctKb  = _if(@КартБВнСчет == ?, "", @КартБВнСчет);
         
         IF (@iCrtType == "К1")  THEN @oAcctK = @__acctK1; ELSE nop ENDIF;
         IF (@iCrtType == "К2")  THEN @oAcctK = @__acctK2; ELSE nop ENDIF;
         IF (@iCrtType == "КБС") THEN @oAcctK = @__acctKb; ELSE nop ENDIF;
         
         @__currK = "";
         
         IF (@iCrtType == "") THEN
           @__docr  = 0;
         ELSE (
           IF (@oAcctK == "") THEN
             @__docr  = 1
           ELSE (
             IF (РЕКВИЗИТ("acct",@oAcctK,"close-date","") <> ?) THEN
               @oAcctK  = "";
               @__docr  = 1;
             ELSE
               @__currK = ENTRY(2, @oAcctK);
               @oAcctK  = ENTRY(1, @oAcctK);
               @__docr  = 0;
             ENDIF
           ) ENDIF
         ) ENDIF;
      END AFTER.

   END TEMPLATE. // ШАБЛОН:   10  - КОНЕЦ

   20:
   DO TEMPLATE CLASS acctb ACTION Нет действий ROLE Ввод данных:
      /// <tm_details>
      /// Новый номер счета
      /// </tm_details>

      /// <tm_AttrOptimized>
      /// Нет
      /// </tm_AttrOptimized>

      DO BEFORE:
         @__docr
      END BEFORE.


      DO AFTER:
         @__acctmask = _if(iCrtType == "К2","90902","90901")
                     + "810к" + "ссссссссссс";
         
         @oAcctK     = НОМЕР_СЧЕТА("accto",
                       _if(iCrtType == "К2",90902,90901),
                       "",?,@__custcat,@__custid,@__acctmask,@__branch);
      END AFTER.

   END TEMPLATE. // ШАБЛОН:   20  - КОНЕЦ

   30:
   DO TEMPLATE CLASS accto ACTION Создание ROLE Ввод данных NO-TRANSACTION:
      /// <tm_details>
      /// Создание счета Картотеки
      /// </tm_details>

      /// <tm_AttrOptimized>
      /// Нет
      /// </tm_AttrOptimized>

      DO BEFORE:
         @__docr
      END BEFORE.

      // РЕКВИЗИТ: acct (формула) #1
      @acct = @oAcctK

      // РЕКВИЗИТ: bal-acct (формула) #2
      @bal-acct = _if(iCrtType == "К2",90902,90901)

      // РЕКВИЗИТ: kau-id (формула) #3
      @kau-id =
      DO:
         _if(@iCrtType == "К2", "Карт-ка2",
                  _if(@iCrtType == "К1", "Карт-ка1", "КартБлСч"))
      END.

      // РЕКВИЗИТ: acct-cat (константа) #4
      @acct-cat = [o]
      // РЕКВИЗИТ: side (константа) #5
      @side = [А]
      // РЕКВИЗИТ: branch-id (формула) #6
      @branch-id = @__branch

      // РЕКВИЗИТ: cust-cat (формула) #7
      @cust-cat = @__custcat

      // РЕКВИЗИТ: cust-id (формула) #8
      @cust-id = @__custid

      // РЕКВИЗИТ: contract (формула) #9
      @contract =
      DO:
         _if(@iCrtType == "К2", "Карт2",
                  _if(@iCrtType == "К1", "Карт1", "КартБл"))
      END.

      // РЕКВИЗИТ: currency (константа) #10
      @currency = []
      // РЕКВИЗИТ: Details (константа) #11
      @Details = []

      DO AFTER:
         @__upd   = UPDATESIGNS("acctb", @iAcct + "," + @iCurrency,
                      _if(@iCrtType == "К1",  "Карт1ВнСчет",
                      _if(@iCrtType == "К2",  "Карт2ВнСчет", "КартБВнСчет")),
                      @oAcctK + ",");
         @__mess  = "      Создан счет " + @iCrtType + " = "
                  + @oAcctK + " |" + @__upd;
         ТРАНЗАКЦИЯ(sbLogIt);
      END AFTER.

   END TEMPLATE. // ШАБЛОН:   30  - КОНЕЦ

END TRANSACTION. // ТРАНЗАКЦИЯ sbk_acct - КОНЕЦ 
// ***************************************************************************




