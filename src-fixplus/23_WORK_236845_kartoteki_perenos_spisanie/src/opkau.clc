/*
     Filename: OPKAU.CLC
      Comment: Выч.поля для браузера балансовых документов по картотеке.
   Parameters:
         Uses:
      Used by: opkau-br.p
      Created: 31.01.2009 Sami
*/

mAcct = GetXattrValueEx("op",string(op.op),"acctbal",?).
mBic = "".
mPerenos = "".
mDocDate = ?.
mSummaRub  = 0.
mSumma = 0.
mSummaOst = 0.
mNumKau = "".
mAcctStat = "".

FOR FIRST op-bank OF op NO-LOCK:
    mBic = op-bank.bank-code.
END.

FIND LAST signs WHERE
          signs.file-name   EQ "op"
      AND signs.code        EQ "op-bal"
      AND signs.xattr-value EQ STRING(op.op)
    NO-LOCK NO-ERROR.
IF AVAIL signs THEN
DO:
   FIND FIRST lop WHERE
              lop.op EQ INTEGER(signs.surrogate)
       NO-LOCK NO-ERROR.
   IF AVAIL lop THEN
   DO:
      FIND FIRST lop-entry OF lop NO-LOCK NO-ERROR.
      IF AVAIL lop-entry THEN
      DO:
         mSummaRub  = lop-entry.amt-rub.
         mSumma = IF lop-entry.currency NE "" THEN lop-entry.amt-cur ELSE lop-entry.amt-rub.

         mNumKau = lop-entry.kau-db.
         IF      lop-entry.kau-cr NE "" 
             AND lop-entry.kau-cr NE lop-entry.kau-db
             AND lop-entry.kau-db EQ "" THEN
         DO:
             mNumKau = lop-entry.kau-cr.
         END.

         /* ищем kau */
         FIND FIRST kau WHERE
                    kau.kau EQ mNumKau
                AND CAN-DO(mTypeKart,kau.kau-id)
             NO-LOCK NO-ERROR.
         IF AVAIL kau THEN
         DO:
            mSummaOst = IF kau.curr-bal EQ 0 AND kau.balance NE 0 THEN kau.balance ELSE kau.curr-bal.

            IF mNumKau NE (STRING(lop-entry.op) + "," + STRING(lop-entry.op-entry)) THEN
            DO:
               FIND FIRST nkau WHERE
                          nkau.kau    EQ mNumKau
                      AND nkau.kau-id NE kau.kau-id
                   NO-LOCK NO-ERROR.
               IF AVAIL nkau THEN
                   mPerenos = "с  " + nkau.kau-id.
               ELSE
                   mPerenos = "!".
            END.

            /* Перенос */
            IF lop-entry.kau-cr NE "" AND lop-entry.kau-cr NE lop-entry.kau-db THEN
            DO:
               FIND FIRST nkau WHERE
                          nkau.kau    EQ lop-entry.kau-db
                      AND nkau.kau-id NE kau.kau-id
                   NO-LOCK NO-ERROR.
               IF AVAIL nkau THEN
                   mPerenos = "на " + nkau.kau-id.
               ELSE
               DO:
                   FIND FIRST nkau WHERE
                              nkau.kau    EQ lop-entry.kau-cr
                          AND nkau.kau-id NE kau.kau-id
                       NO-LOCK NO-ERROR.
                   IF AVAIL nkau THEN
                       mPerenos = "c " + nkau.kau-id.
                   ELSE
                       mPerenos = "!".
               END.
            END.

            /* Дата план. ... только для картотеки 1 */
            IF kau.kau-id EQ "Карт-ка1" THEN
            DO:
               IF SUBSTRING(ENTRY(1, kau.sort),5,1) EQ "." 
                   AND SUBSTRING(ENTRY(1, kau.sort),8,1) EQ "." THEN
               DO:
    	           mDocDate = DATE(SUBSTRING(ENTRY(1,kau.sort),9,2) + "/" 
                            + SUBSTRING(ENTRY(1,kau.sort),6,2) + "/" 
                            + SUBSTRING(ENTRY(1,kau.sort),3,2)) NO-ERROR.
               END.
	           ELSE
	               mDocDate = DATE(TRIM(ENTRY(1,kau.sort))) NO-ERROR.
            END.
         END. /* IF AVAIL kau THEN */

         /*----------- Небольшая вставочка: вывод статуса счета ------------- */
         FIND FIRST acct WHERE
                    acct.acct EQ mAcct
             NO-LOCK NO-ERROR.
         IF AVAIL acct THEN
         DO:
             FIND LAST blockobject WHERE
                      (blockobject.end-datetime EQ ? OR blockobject.end-datetime GE DATETIME(TODAY,MTIME))
                   AND blockobject.class-code EQ "BlockAcct"
                   AND blockobject.file-name  EQ "acct"
                   AND blockobject.surrogate  EQ STRING(acct.acct + "," + acct.currency)
                 NO-LOCK NO-ERROR.
             IF AVAIL blockobject THEN
                 mAcctStat = "Блокирован".

             /*-------  Небольшая вставочка: возвращаем остаток на счете ---------*/
             RUN acct-pos IN h_base (acct.acct,acct.currency,gend-date,gend-date,FGetSetting("СтандТр","AccessStatus","П")).
             mPos = IF acct.currency EQ "" THEN sh-bal ELSE sh-val.

             IF (acct.side EQ "А" AND mPos LT 0) OR (acct.side EQ "П" AND mPos GT 0) THEN
                 mPos = 0.
             ELSE 
                 mPos = ABSOLUTE(mPos).

             /*----------------------- Конец вставочки ---------------------------*/
         END.
      END. /* IF AVAIL lop-entry THEN */
   END. /* IF AVAIL lop THEN */
END. /* IF AVAIL signs THEN */
